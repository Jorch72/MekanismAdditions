defaultTasks 'fullBuild'

// For those who want the bleeding edge
buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
		maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
		}
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}

apply plugin: 'idea'

idea {
    module {
        // For some reason this is necessary for IDEA 2014 workspaces to recognize assets
        inheritOutputDirs = true
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'

repositories {
    maven { // JEI
        url "http://dvs1.progwml6.com/files/maven"
    }
    maven {
        name 'OC Repo'
        url "http://maven.cil.li/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/repositories/snapshots/"
    }
    maven {
        name = "bdew"
        url = "https://jenkins.bdew.net/maven"
    }
    maven {
        name 'amadornes'
        url "http://maven.amadornes.com/"
    }
}

// Define properties file
ext.configFile = file "build.properties"

configFile.withReader {
    // Load config. It shall from now be referenced as simply config or project.config
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

version = "${config.minecraft_version}-${config.mod_version}." + (System.env.BUILD_NUMBER ?: "homebaked")
group = "mekanism.additions"
archivesBaseName = "MekanismAdditions"
libsDirName = "../output"
distsDirName = "../output"
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

minecraft {
    version = config.minecraft_version + "-" + config.forge_version
    mappings = "${config.mappings_version}"
    runDir = "run"
    useDepAts = true
}

dependencies {
    deobfCompile "mezz.jei:jei_1.10.2:3.7.3.223"
    compile "net.bdew.thirdparty:mekanism:1.10.2-9.2.0.2:api"
}

processResources {
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
        exclude '**/*.blend*'
    }
}


task additionsJar(type: Jar) {
    baseName = 'MekanismAdditions'

    from('etc/additions') {
        include '*.info'
        expand 'version':config.mod_version,'mc_version':config.minecraft_version,'mcmp_version':config.mcmp_version
    }

    from zipTree(jar.outputs.getFiles().getSingleFile()).matching {
        include 'mekanism/additions/**', 'assets/mekanismadditions/**'
    }
}

additionsJar.dependsOn('reobfJar')


task releaseJars(type: Copy) {
    from additionsJar
    rename '-(.*)jar', '.jar'
    rename '-(.*)zip', '.zip'
    into '.'
}

task fullBuild(type: Delete) {
    delete jar
}

fullBuild.dependsOn('releaseJars')
